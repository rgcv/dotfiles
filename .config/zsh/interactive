#!/bin/zsh
# zmodload zsh/zprof
source "$XDG_CONFIG_HOME/shell/interactive"

SAVEHIST=$HISTSIZE
export HISTFILE="$XDG_STATE_HOME/zsh/history"
mkdir -p "${HISTFILE%/*}"

source "$XDG_CONFIG_HOME/zsh/rc.d/opts"
source "$XDG_CONFIG_HOME/zsh/rc.d/keys"

# completion module
autoload -Uz compinit
dump_file="$XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION"
mkdir -p "${dump_file%/*}"
for comp in "$dump_file"(N.mh+24); do
  compinit -d "$dump_file"
done
compinit -C -d "$dump_file"
unset dump_file

# lookup style
zstyle :compinstall filename '${ZDOTDIR:-$HOME}/.zshrc'
zstyle ':completion:*' cache-path '$XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION'
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu select
zstyle ':completion:*' rehash true
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*:descriptions' format %U%B%d%b%u
zstyle ':completion:*:warnings' format '%BNo matches for: %d%b'

# load zsh/nearcolor if 24-bit colors aren't supported
case $COLOR_TERM in
  24bit|truecolor) ;;
  *) [ "${terminfo[colors]}" -eq 16777216 ] || zmodload zsh/nearcolor 2>/dev/null
esac

# prompt module
autoload -Uz promptinit && promptinit
# Go Bullet Trains
if [ -n "$GBT__HOME" ]; then
  PROMPT='$(gbt $?)'
  for prog (docker mysql screen ssh su sudo vagrant)
    typeset -f _$prog >/dev/null && compdef _$prog gbt_$prog=$prog
  unset prog
else
  prompt redhat
fi

# plugins
for p in /usr/share/zsh/plugins/*; do source "$p/${p##*/}.zsh"; done 2>/dev/null
unset p

# functions
fp=/usr/share/zsh/site-functions
[ -d $fp ] && fpath=($fp $fpath)
unset fp

handle_usr1() { trap handle_usr1 USR1; rehash; }
trap handle_usr1 USR1
# zprof
